:Namespace world
    ⎕IO ← 0
    l ← 16 17 16
    box ← ⊃∘.∨/1@0¨l-⍛↑¨1

    ⍝ The maximum possible surface can be visualized as a
    ⍝   3d checkerboard. We need to also account for the edges
    ⍝   of the chunk
    ⍝ This is:
    ⍝  6×⌈2÷⍨×/¯2+l    - Inner
    ⍝  6×(×/-×/⍤-∘2)l  - Outer
    max_faces ← 6×⌈2÷⍨×/¯2+l
    max_faces +← 6×(×/-×/⍤-∘2) l

    ⍝ For each face we need 4 vertices and 6 indices
    ⍝ Furthermore, we need the amount of info per vertex
    ⍝ and size of vertex and index values (f32, u32)
    (vsize isize) ← (4×5×4×max_faces ⋄ 4×6×max_faces)

    ib ← 0

    tb ← 0
    tb_pos ← 0
    tb_size ← 10×vsize
    tb_queue ← ⍬

    chunks ← 0⍴⍨0,l
    cnames ← 'vb' 'idx_cnt' 'ready' 'x' 'z'
    chunk_info ← 0⍴⍨0, ≢cnames

    faces ← [
    0 1 0 ⋄ 0 1 1 ⋄ 0 0 1 ⋄ 0 0 0 ⍝ Left
    1 1 1 ⋄ 1 1 0 ⋄ 1 0 0 ⋄ 1 0 1 ⍝ Right
    1 0 0 ⋄ 0 0 0 ⋄ 0 0 1 ⋄ 1 0 1 ⍝ Bottom
    0 1 0 ⋄ 1 1 0 ⋄ 1 1 1 ⋄ 0 1 1 ⍝ Top
    1 1 0 ⋄ 0 1 0 ⋄ 0 0 0 ⋄ 1 0 0 ⍝ Back
    0 1 1 ⋄ 1 1 1 ⋄ 1 0 1 ⋄ 0 0 1 ⍝ Front
    ]
    v_cnt ← ≢faces

    textures ← (
        ⍝ Air (Here for easy indexing, filtered out)
    [0 0 ⋄ 0 0 ⋄ 0 0 ⋄ 0 0]
    [0 0 ⋄ 0 0 ⋄ 0 0 ⋄ 0 0]
    [0 0 ⋄ 0 0 ⋄ 0 0 ⋄ 0 0]
    [0 0 ⋄ 0 0 ⋄ 0 0 ⋄ 0 0]
    [0 0 ⋄ 0 0 ⋄ 0 0 ⋄ 0 0]
    [0 0 ⋄ 0 0 ⋄ 0 0 ⋄ 0 0]
        ⍝ Grass
    [0.6 0.0 ⋄ 0.8 0.0 ⋄ 0.8 0.5 ⋄ 0.6 0.5]
    [0.6 0.0 ⋄ 0.8 0.0 ⋄ 0.8 0.5 ⋄ 0.6 0.5]
    [0.4 0.0 ⋄ 0.6 0.0 ⋄ 0.6 0.5 ⋄ 0.4 0.5]
    [0.0 0.0 ⋄ 0.2 0.0 ⋄ 0.2 0.5 ⋄ 0.0 0.5]
    [0.6 0.0 ⋄ 0.8 0.0 ⋄ 0.8 0.5 ⋄ 0.6 0.5]
    [0.6 0.0 ⋄ 0.8 0.0 ⋄ 0.8 0.5 ⋄ 0.6 0.5]
        ⍝ Stone
    [0.2 0.0 ⋄ 0.4 0.0 ⋄ 0.4 0.5 ⋄ 0.2 0.5]
    [0.2 0.0 ⋄ 0.4 0.0 ⋄ 0.4 0.5 ⋄ 0.2 0.5]
    [0.2 0.0 ⋄ 0.4 0.0 ⋄ 0.4 0.5 ⋄ 0.2 0.5]
    [0.2 0.0 ⋄ 0.4 0.0 ⋄ 0.4 0.5 ⋄ 0.2 0.5]
    [0.2 0.0 ⋄ 0.4 0.0 ⋄ 0.4 0.5 ⋄ 0.2 0.5]
    [0.2 0.0 ⋄ 0.4 0.0 ⋄ 0.4 0.5 ⋄ 0.2 0.5]
        ⍝ Wood
    [0.8 0.0 ⋄ 1.0 0.0 ⋄ 1.0 0.5 ⋄ 0.8 0.5]
    [0.8 0.0 ⋄ 1.0 0.0 ⋄ 1.0 0.5 ⋄ 0.8 0.5]
    [0.8 0.0 ⋄ 1.0 0.0 ⋄ 1.0 0.5 ⋄ 0.8 0.5]
    [0.8 0.0 ⋄ 1.0 0.0 ⋄ 1.0 0.5 ⋄ 0.8 0.5]
    [0.8 0.0 ⋄ 1.0 0.0 ⋄ 1.0 0.5 ⋄ 0.8 0.5]
    [0.8 0.0 ⋄ 1.0 0.0 ⋄ 1.0 0.5 ⋄ 0.8 0.5]
    )
    ⍝ From ⍳-vertex count in shape of l:
    ⍝ - Extend into 3d coordinate space with ⊤
    ⍝ - Transpose into format where position is last and insert rank
    ⍝ - Replicate added rank by number of vertices in a cube
    ⍝ - Add cube vertices to extend each position
    ⍝ - Join in the form (Vertices, 3) so that color/uv/etc can be cated
    vertices ← ,[⍳4] faces(+⍤2) v_cnt(⌿⍤2),[2.5] 3 0 1 2⍉l⊤⊢l⍴⍳×/l
    indices←,0 1 2 0 2 3∘.+⍨4×⍳4÷⍨≢vertices

    ∇ Init_chunks device;tb_params;p;cmd;pass
      tb_params←⊂#.SDL_GPU_TRANSFERBUFFERUSAGE_UPLOAD(tb_size)0
      tb←#.SDL_CreateGPUTransferBuffer device tb_params
      'Error creating chunk transfer buffer'#.Assert tb
     
      ib←#.SDL_CreateGPUBuffer device(#.SDL_GPU_BUFFERUSAGE_INDEX isize 0)
     
      ⍝ Do copy pass just to copy index buffer over
      ⍝ Only need isize worth of indices by reasoning
      ⍝ Overhead should be minimal if this is done once
      p←#.SDL_MapGPUTransferBuffer device tb 0
      #.LSE_MemcpyU32 p(indices)isize
      #.SDL_UnmapGPUTransferBuffer device tb
      cmd←#.SDL_AcquireGPUCommandBuffer device
      pass←#.SDL_BeginGPUCopyPass cmd
      #.SDL_UploadToGPUBuffer pass(tb 0)(ib 0 isize)0
      #.SDL_EndGPUCopyPass pass
      #.SDL_SubmitGPUCommandBuffer cmd
    ∇

    ∇ Queue_chunk(device blocks trans);x;z;vb
      (x z)←trans
      vb←#.SDL_CreateGPUBuffer device(#.SDL_GPU_BUFFERUSAGE_VERTEX vsize 0)
      'Error creating chunk vertex and index buffers'#.Assert vb
     
      chunk_info⍪←vb 0 0 x z
      chunks⍪←blocks
      tb_queue,←⊂x z
     
    ∇

    ⍝ Deprecated
    ∇ OLD_Create_chunk(device blocks trans);x;z;flat;solid;edges;rot;exposed;vert;ind;tex;buf;p;vb;vs
      (x z)←trans×(⊂0 2)⌷l
      flat←∊blocks
      solid←blocks≠0
      edges←solid∧box
      rot←0 1 2∘.{⍵⌽[⍺]solid}¯1 1
      exposed←↑[0]{edges∨solid>⍵}¨rot
     
      vert←(x 0 z)(+⍤1)vertices⌿⍨4/∊exposed
      ⍝vert ← (4/∊exposed)⌿vertices
      ind←,0 1 2 0 2 3∘.+⍨4×⍳4÷⍨≢vert
      tex←(⍳×/l)(×⍤¯1),[1+⍳2],[⍳3]exposed
      tex←(⍳6)(+⍤1)6×flat[tex]
      tex←,[⍳2]↑(∊exposed)/,textures[tex]
      buf←∊vert,tex
     
      (vs is)←(4×≢buf ⋄ 4×≢ind)
      p←#.SDL_MapGPUTransferBuffer device tb 1
      'Error mapping transfer buffer'#.Assert p
      #.LSE_MemcpyF32(p+tb_pos)buf vs
      #.SDL_UnmapGPUTransferBuffer device tb
     
      vb←#.SDL_CreateGPUBuffer device(#.SDL_GPU_BUFFERUSAGE_VERTEX vsize 0)
      'Error creating chunk vertex buffer'#.Assert vb
     
      tb_queue,←⊂(≢chunk_info)tb vb vs tb_pos
      tb_pos+←vs+is
      chunk_info⍪←vb(≢ind)0 x z
      chunks⍪←blocks
    ∇

    ∇ Copy_chunks(device pass);q;x;z;tx;tz;m;blks;flat;solid;edges;rot;exposed;vert;ind;tex;buf;vs;ic;p;pos;i;vb
      tb_pos←0
     

      q←⍬
      p←#.SDL_MapGPUTransferBuffer device tb 1
      :While (tb_size>tb_pos+vsize)∧(0≠≢tb_queue)
          (x z)←⊃1↑tb_queue
          (tx tz)←(x z)×(⊂0 2)⌷l
     
          m←∧/(x z)(=⍤1)(⊂cnames⍳'x' 'z')⌷[1]chunk_info
          blks←,[⍳2]m⌿chunks ⍝squeeze (1, ...
          flat←∊blks
          solid←blks≠0
          edges←solid∧box
          rot←0 1 2∘.{⍵⌽[⍺]solid}¯1 1
          exposed←↑[0]{edges∨solid>⍵}¨rot
     
          vert←(tx 0 tz)(+⍤1)vertices⌿⍨4/∊exposed
          ind←,0 1 2 0 2 3∘.+⍨4×⍳4÷⍨≢vert
          tex←(⍳×/l)(×⍤¯1),[1+⍳2],[⍳3]exposed
          tex←(⍳6)(+⍤1)6×flat[tex]
          tex←,[⍳2]↑(∊exposed)/,textures[tex]
          buf←∊vert,tex
          vs←4×≢buf
          #.LSE_MemcpyF32(p+tb_pos)buf vs
     
          q,←⊂tb_pos vs(≢ind)(⍸m)
          tb_queue←1↓tb_queue
          tb_pos+←vs
      :EndWhile
      #.SDL_UnmapGPUTransferBuffer device tb
     
      :For (pos vs ic i) :In q
          vb←(i 0)⌷chunk_info
          #.SDL_UploadToGPUBuffer pass(tb pos)(vb 0 vs)1
          ((i 1)⌷chunk_info)←ic
          ((i 2)⌷chunk_info)←1
      :EndFor
    ∇

    ⍝ Deprecated
    ∇ OLD_Copy_chunks(device pass);idx;tb;vb;ib;vs;pos
      :For (idx tb vb vs pos) :In tb_queue
          #.SDL_UploadToGPUBuffer pass(tb pos)(vb 0 vs)1
          (idx(cnames⍳⊂'ready')⌷chunk_info)←1
          ⎕←pos vs
      :EndFor
    ∇

    ∇ Draw_chunks(device pass);vb;ind;ready;x;z;start;list;vbs;inds
      #.SDL_BindGPUIndexBuffer pass(⊂ib 0)#.SDL_GPU_INDEXELEMENTSIZE_32BIT
      list ← {1=(cnames⍳⊂'ready')⌷⍵}¨⍛/↓chunk_info
      vbs ← ⍬
      inds ← ⍬
      :For (vb ind ready x z) :In list 
        vbs,← vb
        inds,← ind         
      :EndFor
      ⍝ A lot of ⎕NA's at once slows down
      #.LSE_DrawList pass vbs inds (≢vbs)

    ∇

:EndNamespace
