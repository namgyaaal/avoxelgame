:Namespace easy_na

      Clean_def←{
          ⍝ Cases where <>= have a space after the struct/type they're on.
          ⍝ One-letter characters are too weird to handle properly, for now
          ⍝ just use '-' in the definition and collapse it after done with
          ⍝ parsing
          ⍝ Dirty, but it works
          ⍵/⍨(⍵='-')⍱(⍵=' ')∧(¯1⌽⍵∊'<>=')∨(1⌽⍵∊'[]')
      }

      Build_args←{
          ⍝ Recursively go down a nested vector and unroll it to produce
          ⍝     a definition that fits ⎕NA format.
          (⍬≡⍵):⍬
          2=|≡⍵:¯1↓∊⍵,¨' '  ⍝ flat-vector
          2>|≡⍵:⍵           ⍝ 1-itemed
     
          ⍝ Separate scalars at the level from vectors and recurse down vectors
          ⍝ Concat all at the end
          deep←1<|≡¨⍵
          args←⍵
          (deep/args)←('{',Build_args,'}'⍨)¨deep/args
          ¯1↓∊args,¨' '
      }

      Load_def←{
          102:: 'Error: File not found for ',⍵
          6:: 'Error: Function not found in library for ',⍵
          _←⎕NA ⍵
          ''
      }
      Safe_load←{
          ⍝ ⍺ - (namespace, library_path)
          ⍝ ⍵ - symbol vector
          error_vec ← ⍺∘{
              (fn_name def args)←⍵
              formatted←∊⍺,'|',fn_name
              def←1↓∊' '∘,¨(Clean_def def)formatted(Clean_def Build_args args)
              res←fn_name Load_def def
              0≠≢res: res
              _←'#'⎕NS fn_name
              res
          }¨⍵
          error_vec ← error_vec/⍨0≠≢¨error_vec
          0=≢error_vec: ⋄
          {⎕ ← ⍵}¨error_vec
      }

      Example_test←{
          ⍝ requires dll existing in this path to work
          i32←'I4'
          f32←'F4'
          ptr←'P'
          pair←(i32 i32)
     
          real_dylib←'libbasic.dylib'
          str←'<0T1[]'
          symbol_vector←(
              'print'⍬ ⍬
              'print_str'⍬ str
              'call_with_side_effects'⍬ ⍬
              'get_side_effects'i32 ⍬
              'call_with_input'i32 i32
          )
          real_dylib Safe_load symbol_vector
          
      }
:EndNamespace
