:Namespace world
    ⎕IO ← 0
    l ← 16 32 16
    box ← ⊃∘.∨/1@0¨l-⍛↑¨1

    ⍝ The maximum possible surface can be visualized as a
    ⍝   3d checkerboard. We need to also account for the edges
    ⍝   of the chunk
    ⍝ This is:
    ⍝  6×⌈2÷⍨×/¯2+l    - Inner
    ⍝  6×(×/-×/⍤-∘2)l  - Outer
    max_faces ← 6×⌈2÷⍨×/¯2+l
    max_faces +← 6×(×/-×/⍤-∘2) l

    ⍝ For each face we need 4 vertices and 6 indices
    ⍝ Furthermore, we need the amount of info per vertex and size of indices
    (vsize isize) ← (4×4×max_faces ⋄ 4×6×max_faces)

    vb_pool ← ⍬
    ib ← 0
    tb ← 0
    tb_pos ← 0
    tb_size ← 5×vsize
    tb_queue ← ⍬
    (texture sampler) ← ⍬ ⍬


    chunks ← 0⍴⍨0,l
    cnames ← 'xz' 'vb' 'idx_cnt' 'ready'
    chunk_info ← 0⍴⍨0, ≢cnames

    view_distance ← 8
    max_chunks ← ≢view_distance>0.5*⍨+/¨2*⍨,∘.,⍨((-view_distance)+⍳1+2×view_distance)
    atlas_len ← 5 ⍝ Texture atlas length


    I←⌷⍨∘⊃⍨⍤0 99
    ⍝ Chunk column index (Only used with one entry)
    Ci ← cnames∘⍳∘⊂
    ⍝ Chunk get
    Cg ← {chunk_info⌷[1]⍨Ci⍵}
    ⍝ Chunk get cell (Where ⍺ = row #, ⍵ = column name)
    Cgc ← {chunk_info⌷⍨⍺,Ci⍵}
    ⍝ Chunk column lookup (Where ⍺ = entries, ⍵ = column name)
    Cl ← {⍺ (≡⍤0) chunk_info⌷[1]⍨Ci⍵}

    faces ← [
    0 1 0 ⋄ 0 1 1 ⋄ 0 0 1 ⋄ 0 0 0 ⍝ Left
    1 1 1 ⋄ 1 1 0 ⋄ 1 0 0 ⋄ 1 0 1 ⍝ Right
    1 0 0 ⋄ 0 0 0 ⋄ 0 0 1 ⋄ 1 0 1 ⍝ Bottom
    0 1 0 ⋄ 1 1 0 ⋄ 1 1 1 ⋄ 0 1 1 ⍝ Top
    1 1 0 ⋄ 0 1 0 ⋄ 0 0 0 ⋄ 1 0 0 ⍝ Back
    0 1 1 ⋄ 1 1 1 ⋄ 1 0 1 ⋄ 0 0 1 ⍝ Front
    ]
    v_cnt ← ≢faces

    textures ← ∊(
        ⍝ Air (Here for easy indexing, filtered out)
    0 0 0 0 0 0
        ⍝ Grass
    3 3 2 0 3 3
        ⍝ Stone
    1 1 1 1 1 1
        ⍝ Wood
    5 5 5 5 5 5
        ⍝ Log
    9 9 9 9 9 9
        ⍝ Leaf
    10 10 10 10 10 10
    )
    ⍝ From ⍳-vertex count in shape of l:
    ⍝ - Extend into 3d coordinate space with ⊤
    ⍝ - Transpose into format where position is last and insert rank
    ⍝ - Replicate added rank by number of vertices in a cube
    ⍝ - Add cube vertices to extend each position
    ⍝ - Join in the form (Vertices, 3) so that color/uv/etc can be cated
    vertices ← ,[⍳4] faces(+⍤2) v_cnt(⌿⍤2),[2.5] 3 0 1 2⍉l⊤⊢l⍴⍳×/l
    indices←,0 1 2 0 2 3∘.+⍨4×⍳4÷⍨≢vertices

      Transform_points←{
          coords←⌊l[0]÷⍨(⊂0 2)∘⌷¨⍵
          cindices←coords⍳⍨Cg'xz'
          cindices,¨16|⍵
      }

    ∇ info←Create_block_texture(device ptr);size;surface_type;img;surface;full_w;full_h;w;h;tex_params;section
      ⍝ Magic number for 8 8 8 8 A B G R texture
      surface_type←376840196
      img←##.IMG_Load⊂'assets/blocks.bmp'
     
      surface←##.SDL_ConvertSurface img surface_type
      'Error loading chunk image'##.Assert img surface
      ##.SDL_DestroySurface img
      (full_w full_h _ _)←##.LSE_GetSurfaceParams surface 0 0 0 0
      (w h)←atlas_len÷⍨(full_w full_h)
     
      tex_params←⊂1 ##.SDL_GPU_TEXTUREFORMAT_R8G8B8A8_UNORM 3 w h(atlas_len*2)4 0 0
      texture←##.SDL_CreateGPUTexture device tex_params
      'Error creating chunk texture'##.Assert texture
      size←0
      {
          (x y)←(w h)×⍵
          section←##.SDL_CreateSurface w h surface_type
          r1←##.SDL_UnlockSurface section
          r2←##.SDL_BlitSurface surface(⊂x y w h)section(⊂0 0 w h)
          _←##.SDL_LockSurface section
          (w h pitch _)←##.LSE_GetSurfaceParams section 0 0 0 0
          size⊢←h×pitch
          addr←##.LSE_GetSurfaceDataAddress section
          offset←size×(0⊃⍵)+atlas_len×(1⊃⍵)
          _←##.SDL_memcpy(ptr+offset)(addr)size
          _←##.SDL_DestroySurface section
          'Error loading textures onto GPU'##.Assert r1 r2
      }¨∘.,⍨⍳atlas_len
      ##.SDL_DestroySurface surface
      ⍝ return so that reading onto texture in the GPU has reference
      info←(w h size)
    ∇

    ∇ Init_chunks device;vb_params;tb_params;p;cmd;pass;sam_params;w;h;pitch;addr;size
      vb_params←##.SDL_GPU_BUFFERUSAGE_VERTEX vsize 0
      vb_pool←({⍵,##.SDL_CreateGPUBuffer device vb_params}⍣(⌊1.2×max_chunks))⍬
      'Error creating vertex buffer pool'##.Assert vb_pool
     
      tb_params←⊂##.SDL_GPU_TRANSFERBUFFERUSAGE_UPLOAD(tb_size)0
      tb←##.SDL_CreateGPUTransferBuffer device tb_params
      'Error creating chunk transfer buffer'##.Assert tb
      ib←##.SDL_CreateGPUBuffer device(##.SDL_GPU_BUFFERUSAGE_INDEX isize 0)
     
      sam_params←⊂0 0 0 2 2 2 0 0 0 0 100 0 0 0 0 0
      sampler←##.SDL_CreateGPUSampler device sam_params
      'Error creating chunk sampler'##.Assert sampler
      ⍝ Do copy pass just to copy index buffer over
      ⍝ Only need isize worth of indices by reasoning
      ⍝ Overhead should be minimal if this is done once
      p←##.SDL_MapGPUTransferBuffer device tb 0
      ##.LSE_MemcpyU32 p(indices)isize
      (w h size)←Create_block_texture device(p+isize)
     
      ##.SDL_UnmapGPUTransferBuffer device tb
      cmd←##.SDL_AcquireGPUCommandBuffer device
      pass←##.SDL_BeginGPUCopyPass cmd
     
      ##.SDL_UploadToGPUBuffer pass(tb 0)(ib 0 isize)0
      {
          (x y)←⍵
          offset←x+atlas_len×y
          ##.SDL_UploadToGPUTexture(
              pass
              tb(isize+size×offset)0 0
              texture 0(offset)0 0 0 w h 1
              0
          )
      }¨∘.,⍨⍳atlas_len
     
      ##.SDL_EndGPUCopyPass pass
      ##.SDL_GenerateMipmapsForGPUTexture cmd texture
      ##.SDL_SubmitGPUCommandBuffer cmd
    ∇

    ∇ Delete device
      ##.SDL_ReleaseGPUTexture device texture
      ##.SDL_ReleaseGPUSampler device sampler
    ∇

    ∇ Queue_chunk(device blocks trans);x;z;vb
      (x z)←trans
     
      :If 0<≢vb_pool
          vb←1↑vb_pool
          vb_pool←1↓vb_pool
      :Else
          vb←##.SDL_CreateGPUBuffer device(##.SDL_GPU_BUFFERUSAGE_VERTEX vsize 0)
          'Error creating vertex buffer'##.Assert vb
      :EndIf
      chunk_info⍪←(x z)vb 0 0
      chunks⍪←blocks
      tb_queue,←⊂x z
    ∇

    ∇ info←Copy_chunk(chunk tx tz ptr offset);tx;tz;flat;solid;edges;exposed;m;vert;tex;buf;vs;ic
      ⍝ Collapse chunk @ x z and write it to ptr
      ⍝ Return vertex size in bytes and number of indices
      ⍝ IMPORTANT: Assumes that where data is being written to has enough space
      flat←∊chunk
      solid←chunk≠0
      edges←solid∧box
     
      ⍝ Returns format (16 32 16 3 2) where (3 2) are rotations
      exposed←↑[0]{edges∨solid>⍵}¨0 1 2∘.{⍵⌽[⍺]solid}¯1 1
     
      vert←vertices⌿⍨4/m←∊exposed
      ⍝ collapse into (vertex_num 6) multiply by index where not 0
      tex←(⍳×/l)(×⍤¯1),[1+⍳2],[⍳3]exposed
      ⍝ index into box and 6wise multiply+⍳add for texture index
      tex←∊(⍳6)(+⍤1)6×flat[tex]
      ⍝ index into texture and select exposed faces, duplicate 4 for faces
      tex←4/m/,textures[tex]
      ⍝ do some bitpacking here. Since we know x z ∊ [0, 16], they only take 5 bits
      ⍝ use the leading bit for uv coordinate into 4 corners (hence adding 128)
      ⍝ with this we only need 4 bytes for a vertex
      vert+←(3,⍨≢vert)⍴128×[0 0 0 ⋄ 0 0 1 ⋄ 1 0 1 ⋄ 1 0 0]
      buf←∊vert,tex
      vs←≢buf
      ic←6×4÷⍨≢vert
      ##.LSE_MemcpyU8(ptr+offset)buf vs
      info←vs ic
    ∇

    ∇ Copy_chunks(device pass);idx;q;x;z;tx;tz;m;is;tex;buf;vs;ic;p;pos;i;vb
      tb_pos←0
      q←⍬
      p←##.SDL_MapGPUTransferBuffer device tb 1
      :While (tb_size>tb_pos+vsize)∧(0≠≢tb_queue)
          (x z)←⊃1↑tb_queue
          idx←⊃⍸(⊂x z)Cl'xz'
          (tx tz)←(x z)×(⊂0 2)⌷l
          (vs ic)←Copy_chunk(idx⌷chunks)tx tz p tb_pos
     
          q,←⊂tb_pos vs ic idx
          tb_queue←1↓tb_queue
          tb_pos+←vs
      :EndWhile
      ##.SDL_UnmapGPUTransferBuffer device tb
     
      :For (pos vs ic i) :In q
          vb←i Cgc'vb'
          ##.SDL_UploadToGPUBuffer pass(tb pos)(vb 0 vs)0
          ((i,Ci'idx_cnt')⌷chunk_info)←ic
          ((i,Ci'ready')⌷chunk_info)←1
      :EndFor
    ∇

    ∇ Update_range(device x z);range;dist;new;old;m;pair;tx;tz;ty
      range←,∘.,⍨((-view_distance)+⍳1+2×view_distance)
      range←(⊂x z)+range/⍨∊view_distance≥0.5*⍨+/¨range*2
     
      new←range~Cg'xz'
      old←range~⍨Cg'xz'
     
      chunks←chunks⌿⍨m←~old∊⍨Cg'xz'
      vb_pool,←(~m)/Cg'vb'
      chunk_info←m⌿chunk_info
     
      :For pair :In new
          blocks←l⍴0
          blocks[;0;]←1
          ⍝ Tree :)
          tx tz←4+?10 10
          ty←3+?3
          blocks[tx;1+⍳ty;tz]←4
          blocks[tx+¯2+⍳5;1 2+ty;tz+¯2+⍳5]←5
          blocks[tx+¯1+⍳3;3 4+ty;tz+¯1+⍳3]←5
          blocks[?14;1;?14]←2
          Queue_chunk device blocks pair
      :EndFor
    ∇

    ∇ Draw_chunks(cmd_buf pass px pz lx lz proj_view);vec;dist;num;den;m;vbs;inds;xz;ud
      vec←(⊂px pz)-⍨Cg'xz'
      dist←0.5*⍨+/¨vec*2
      num←(lx lz)(+.×⍤1)↑vec
      den←dist×0.5*⍨+/(lx lz)*2
      ⍝ Filter out zero-dist chunk and re-add
      m←(0≠den)∧Cg'ready'
      (m/m)←(○0.8)>¯2○(m⌿num)÷(m⌿den)
      m←(Cg'ready')∧(m∨0=den)
      vbs←m/Cg'vb'
      inds←m/Cg'idx_cnt'
      ⍝ translation matrix to make MVP for each chunk and pass as a push constant
      ⍝ suspect some better way to do this with rank?
      ud←∊{(x z)←⍵ ⋄ proj_view+.×⍨(x 0 z 1)@3⊢∘.=⍨⍳4}¨(⊃l)×m/Cg'xz'
      ##.SDL_BindGPUFragmentSamplers pass 0(⊂texture sampler)1
      ##.SDL_BindGPUIndexBuffer pass(⊂ib 0)##.SDL_GPU_INDEXELEMENTSIZE_32BIT
      ##.LSE_DrawListWithPushConstants cmd_buf pass vbs inds(≢vbs)ud 0 16
     
    ∇

:EndNamespace
