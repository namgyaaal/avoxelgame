:Namespace text
    ⎕IO←0
    (max_vertices max_indices) ← (4000 3000)

    font ← 0
    engine ← 0
    pipeline ← 0
    sampler ← 0

    font_vb ← 0
    font_ib ← 0
    font_tb ← 0

    sequences ← ⍬

    ⍝ Inverted Table
    ⍝ - Text Pointer
    ⍝ - Text Data ('UTF-8'⎕UCS)
    ⍝ - Translation
    ⍝ - Scale
    ⍝ - Color (rgba)
    tnames ← 'ptr' 'str' 'xy' 'scale' 'color'
    table ← ⍬ ⍬ ⍬ ⍬ ⍬

    Ti ← tnames∘⍳∘⊂

    ∇ Init(device window);res;sam_params
      res←##.TTF_Init
      'Error initializing SDL3_ttf'##.Assert res
      font←##.TTF_OpenFont'assets/APL387.ttf' 64
      'Error loading APL387 font'##.Assert font
      ##.TTF_SetFontSDF font 0
      engine←##.TTF_CreateGPUTextEngine device
      'Error creating font engine'##.Assert engine
     
      Create_pipeline device window
     
      font_vb←##.SDL_CreateGPUBuffer device(
          ##.SDL_GPU_BUFFERUSAGE_VERTEX
          36×max_vertices
          0
      )
      font_ib←##.SDL_CreateGPUBuffer device(
          ##.SDL_GPU_BUFFERUSAGE_INDEX
          4×max_indices
          0
      )
      font_tb←##.SDL_CreateGPUTransferBuffer(
          device
          ##.SDL_GPU_TRANSFERBUFFERUSAGE_UPLOAD((36×max_vertices)+(4×max_indices))0
      )
      'Error creating font buffers'##.Assert font_vb font_ib font_tb

      sam_params←⊂1 1 1 2 2 2 0 0 0 0 0 0 0 0 0 0
      sampler←##.SDL_CreateGPUSampler device sam_params
      'Error creating font sampler'##.Assert sampler
    ∇

    ∇ Copy (device pass);p;vbc;ibc
      sequences←##.TTF_GetGPUTextDrawData¨(Ti'ptr')⌷table
      p←##.SDL_MapGPUTransferBuffer device font_tb 1
      vbc ibc←⊃+⌿{##.LSE_FontGPUCopy p ⍵ 0 0 0 0 0 1 max_vertices}¨sequences
      ##.SDL_UnmapGPUTransferBuffer device font_tb

      ##.SDL_UploadToGPUBuffer pass(font_tb 0)(font_vb 0(36×vbc))1
      ##.SDL_UploadToGPUBuffer pass(font_tb(36×max_vertices))(font_ib 0(4×ibc))1
    ∇

    ∇ Draw (cmd_buf pass proj_mat);trans;scale;model
      ##.SDL_BindGPUGraphicsPipeline pass pipeline

      trans ← ↑{(⊃⍵⋄1⊃⍵⋄0⋄1)@3⊢∘.=⍨⍳4}¨table⌷⍨Ti'xy'
      scale ← ↑{1@(⊂3 3)⊢⍵×∘.=⍨⍳4}¨table⌷⍨Ti'scale'

      model ← scale (+.×⍤2 2) trans

      ##.SDL_PushGPUVertexUniformData cmd_buf 0(∊model+.×proj_mat)(16×4)
      ##.SDL_BindGPUVertexBuffers pass 0(⊂font_vb 0)1
      ##.SDL_BindGPUIndexBuffer pass(⊂font_ib 0)##.SDL_GPU_INDEXELEMENTSIZE_32BIT


      {##.LSE_FontGPUDraw pass sampler ⍵}¨sequences

    ∇

    ∇ Release device
      ##.SDL_ReleaseGPUBuffer device font_vb
      ##.SDL_ReleaseGPUBuffer device font_ib
      ##.SDL_ReleaseGPUBuffer device font_tb
      ##.SDL_ReleaseGPUSampler device sampler
      ##.SDL_ReleaseGPUGraphicsPipeline device pipeline
      ##.TTF_DestroyText¨table⌷⍨Ti'ptr'
      ##.TTF_DestroyGPUTextEngine engine
      ##.TTF_CloseFont font
      ##.TTF_Quit
    ∇

    ∇ r←Create_shaders device;v_src;v_size;f_src;f_size;v_info;f_info;entry_point;shader_format;v_shader;f_shader
      (v_src v_size)←##.SDL_LoadFile(##.Get_shader_path'text_vert')0
      (f_src f_size)←##.SDL_LoadFile(##.Get_shader_path'text_frag')0
     
      'Error loading shaders'##.Assert v_src f_src
      shader_format←##.Get_shader_format ⍬
      v_info←v_size v_src 0 shader_format ##.SDL_GPU_SHADERSTAGE_VERTEX 0 0 0 1 0
      f_info←f_size f_src 0 shader_format ##.SDL_GPU_SHADERSTAGE_FRAGMENT 1 0 0 0 0
      entry_point←##.Get_entry_point ⍬
     
      v_shader←##.LSE_CreateGPUShader device v_info entry_point
      f_shader←##.LSE_CreateGPUShader device f_info entry_point
      'Error creating shaders'##.Assert v_shader f_shader
      r←(v_shader f_shader)
    ∇

    ∇ Create_pipeline(device window);v_shader;format;f_shader;vb_desc;vb_attr;blend_state;ct_desc
      (v_shader f_shader)←Create_shaders device
      format←##.SDL_GetGPUSwapchainTextureFormat device window
      ##.LSE_PipelineClearParams ⍬
      ##.LSE_PipelineSetShaders v_shader f_shader
     ⍝ 36=4×≢ x y z r g b a u v
      vb_desc←⊂(0 36 0 0)
      vb_attr←(0 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT3 0
          1 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT4(4×3)
          2 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT2(4×7))
      ##.LSE_PipelineSetVertexInput vb_desc 1 vb_attr 3
     
      blend_state←(
          ##.SDL_GPU_BLENDFACTOR_SRC_ALPHA
          ##.SDL_GPU_BLENDFACTOR_ONE_MINUS_SRC_ALPHA
          ##.SDL_GPU_BLENDOP_ADD
          ##.SDL_GPU_BLENDFACTOR_SRC_ALPHA
          ##.SDL_GPU_BLENDFACTOR_DST_ALPHA
          ##.SDL_GPU_BLENDOP_ADD
      ),16,1,3⍴0
      ct_desc←⊂format blend_state
      ##.LSE_PipelineSetTargetInfo ct_desc 1 0 0
      pipeline←##.LSE_PipelineCreate device
      'Error creating text pipeline'##.Assert pipeline
      ##.SDL_ReleaseGPUShader device v_shader
      ##.SDL_ReleaseGPUShader device f_shader
    ∇
:EndNamespace
