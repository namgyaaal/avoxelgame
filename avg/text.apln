:Namespace text
    'chksum'⎕CY'dfns'
    ⎕IO←0
    (max_vertices max_indices) ← (4000 3000)

    font ← 0
    engine ← 0
    pipeline ← 0
    sampler ← 0

    vb ← 0
    ib ← 0
    tb ← 0

    sequences ← ⍬
    counts ← 0 2⍴0

    ⍝ Table Format
    ⍝ - Text Pointer
    ⍝ - Text Data : 'UTF-8'⎕UCS
    ⍝ - Translation
    ⍝ - Scale
    ⍝ - Rotation
    ⍝ - Color : rgba with (4⍴256)⊥
    ⍝ - Width
    ⍝ - Height
    ⍝ - Centered : translate by size, usful for rotation around center
    ⍝ - Hash : for updating if it changed

    tnames ← 'ptr' 'str' 'x' 'y' 'scale' 'rotation' 'color' 'w' 'h' 'center' 'hash'
    table ← (≢tnames)⍴⊂⍬
    Ti ← tnames∘⍳∘⊂

    ∇ Init(device window);res;sam_params
      res←##.TTF_Init
      'Error initializing SDL3_ttf'##.Assert res
      font←##.TTF_OpenFont'assets/APL387.ttf' 64
      'Error loading APL387 font'##.Assert font
      _←##.TTF_SetFontSDF font 0
      engine←##.TTF_CreateGPUTextEngine device
      'Error creating font engine'##.Assert engine
      Create_pipeline device window
      vb←##.SDL_CreateGPUBuffer device(
          ##.SDL_GPU_BUFFERUSAGE_VERTEX
          36×max_vertices
          0
      )
      ib←##.SDL_CreateGPUBuffer device(
          ##.SDL_GPU_BUFFERUSAGE_INDEX
          4×max_indices
          0
      )
      tb←##.SDL_CreateGPUTransferBuffer(
          device
          ##.SDL_GPU_TRANSFERBUFFERUSAGE_UPLOAD((36×max_vertices)+(4×max_indices))0
      )
      'Error creating font buffers'##.Assert vb ib tb
     
      sam_params←⊂1 1 1 2 2 2 0 0 0 0 0 0 0 0 0 0
      sampler←##.SDL_CreateGPUSampler device sam_params
      'Error creating font sampler'##.Assert sampler
    ∇

    ∇ Copy(device pass);wh_tbl;p;colors;hash;update_mask;vbc;ibc;sum
      :If 0=≢⊃table
          :Return
      :EndIf
      update_mask←(table⊃⍨Ti'hash')≠hashes←chksum↓⍉↑⊃∘table¨tnames⍳'color' 'str'
      wh_tbl←⊃⍪⌿{
          (ptr str)←⍵
          _←{##.TTF_SetTextString ptr str(≢str)}⍬
          (_ w h)←{##.TTF_GetTextSize ptr 0 0}⍬
          [w h ⋄ ]
      }¨↓⍉↑'ptr'{⍺ ⍵}⍥{update_mask/⍵⊃table}⍥Ti'str'
      ((Ti'hash')⊃table)←hashes
      (update_mask/(Ti'w')⊃table)←0⌷⍉wh_tbl
      (update_mask/(Ti'h')⊃table)←1⌷⍉wh_tbl
     
      sequences←##.TTF_GetGPUTextDrawData¨table⊃⍨Ti'ptr'
      p←##.SDL_MapGPUTransferBuffer device tb 1
      colors←↓⍉255÷⍨(4⍴256)⊤table⊃⍨Ti'color'
     
      ⍝ Need to accumulate offsets into the vertex and index buffers
      ⍝     through this since all texts are packed, hence why we do power.
      counts←2⊃{
          (seq(r g b a)cnt)←(1↑⊃⍵ ⋄ ⊃1↑1⊃⍵ ⋄ 2⊃⍵)
          (vo io)←+⌿cnt
          (vbc ibc)←##.LSE_FontGPUCopy p seq 0 0 r g b a max_vertices vo io
          (1↓⊃⍵ ⋄ 1↓1⊃⍵ ⋄ cnt⍪[vbc ibc ⋄ ])
      }⍣{0=≢⊃⍺}⊢(sequences ⋄ colors ⋄ 0 2⍴0)
      (vbc ibc)←+⌿counts
     
     
      _←##.SDL_UnmapGPUTransferBuffer device tb
      _←##.SDL_UploadToGPUBuffer pass(tb 0)(vb 0(36×vbc))1
      _←##.SDL_UploadToGPUBuffer pass(tb(36×max_vertices))(ib 0(4×ibc))1
    ∇

    ∇ Draw(cmd_buf pass proj_mat);origin;rotations;rot;trans;scale;models;x;y;w;h;offsets;centered
      :If 0=≢⊃table
          :Return
      :EndIf
      _←##.SDL_BindGPUGraphicsPipeline pass pipeline
     
      (x y w h centered)←⊃∘table⍤Ti¨('x' 'y' 'w' 'h' 'center')
      origin←↑{(-⊃⍵ ⋄ 1⊃⍵ ⋄ 0 ⋄ 1)@3⊢∘.=⍨⍳4}¨↓⍉↑2÷⍨w h
      ((~centered)⌿origin)←(+/~centered)⌿,[¯0.5]∘.=⍨⍳4
      rot←↑{
          ul←1 ¯1 1 1×2 1 1 2○⍵ ⍝ Rotate on XY plane
          4 4⍴(ul,1,1)@(0 1 4 5 10 15)⊢16⍴0
      }¨table⊃⍨Ti'rotation'
     
      trans←↑{(⊃⍵ ⋄ 1⊃⍵ ⋄ 0 ⋄ 1)@3⊢∘.=⍨⍳4}¨↓⍉↑x y
      scale←↑{1@(⊂3 3)⊢⍵×∘.=⍨⍳4}¨table⊃⍨Ti'scale'
      models←proj_mat+.×⍨⊃(+.×⍤2 2)/origin rot scale trans
      offsets←¯1↓0 0⍪+⍀counts
      _←##.SDL_BindGPUVertexBuffers pass 0(⊂vb 0)1
      _←##.SDL_BindGPUIndexBuffer pass(⊂ib 0)##.SDL_GPU_INDEXELEMENTSIZE_32BIT
      {
          (seq ⋄ mat ⋄ (v i))←⍵
          _←{##.SDL_PushGPUVertexUniformData cmd_buf 0 mat(16×4) ⋄ 0}⍬
          _←##.LSE_FontGPUDraw pass sampler seq v i
     
      }¨↓⍉↑(sequences ⋄ ↓,[1+⍳2]models ⋄ ↓offsets)
     
    ∇

    ∇ Clear device
      _←##.TTF_DestroyText¨table⊃⍨Ti'ptr'
      table←(≢tnames)⍴⊂⍬
    ∇

    ∇ Release device
      Clear device
      _←##.SDL_ReleaseGPUBuffer device vb
      _←##.SDL_ReleaseGPUBuffer device ib
      _←##.SDL_ReleaseGPUBuffer device tb
      _←##.SDL_ReleaseGPUSampler device sampler
      _←##.SDL_ReleaseGPUGraphicsPipeline device pipeline
      _←##.TTF_DestroyGPUTextEngine engine
      _←##.TTF_CloseFont font
      _←##.TTF_Quit
     
      font←0
      engine←0
      pipeline←0
      sampler←0
     
      vb←0
      ib←0
      tb←0
     
      sequences←⍬
      counts←0 2⍴0
      table←(≢tnames)⍴⊂⍬
    ∇

    ∇ Create_pipeline(device window);v_shader;format;f_shader;vb_desc;vb_attr;blend_state;ct_desc
      (v_shader f_shader) ← (v_ub: 1 ⋄ f_sp: 1) ##.Create_shaders device 'text_vert' 'text_frag'
      format←##.SDL_GetGPUSwapchainTextureFormat device window
      _←##.LSE_PipelineClearParams ⍬
      _←##.LSE_PipelineSetShaders v_shader f_shader
     ⍝ 36=4×≢ x y z r g b a u v
      vb_desc←⊂(0 36 0 0)
      vb_attr←(0 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT3 0
          1 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT4(4×3)
          2 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT2(4×7))
      _←##.LSE_PipelineSetVertexInput vb_desc 1 vb_attr 3
     
      blend_state←(
          ##.SDL_GPU_BLENDFACTOR_SRC_ALPHA
          ##.SDL_GPU_BLENDFACTOR_ONE_MINUS_SRC_ALPHA
          ##.SDL_GPU_BLENDOP_ADD
          ##.SDL_GPU_BLENDFACTOR_SRC_ALPHA
          ##.SDL_GPU_BLENDFACTOR_DST_ALPHA
          ##.SDL_GPU_BLENDOP_ADD
      ),16,1,3⍴0


      ct_desc←⊂format blend_state
      _←##.LSE_PipelineSetTargetInfo ct_desc 1 0 0
      pipeline←##.LSE_PipelineCreate device
      'Error creating text pipeline'##.Assert pipeline
      _←##.SDL_ReleaseGPUShader device v_shader
      _←##.SDL_ReleaseGPUShader device f_shader
    ∇
:EndNamespace
