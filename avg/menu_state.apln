:Namespace menu_state
    ⎕IO←0

    pipeline←0
    (apple_texture apple_sampler apple_vb)←0
    (bg_sampler bg_vb)←0
    ib←0


    apple_vs←4×4×5
    bg_vs←4×4×5
    is←2×6

    elapsed←0

    ∇ Init core_data;sam_params;bg_vertices;ptr;tb;cmd;pass;dirt;u;v;txt;text_render;img;img2;enc;texs;apple_vertices;indices;addr
      dirt←2
     
      apple_texture←##.SDL_CreateGPUTexture core_data.device(
          1 ⋄ ##.SDL_GPU_TEXTUREFORMAT_R8G8B8A8_UNORM ⋄ 3 ⋄ 256 ⋄ 256
          1 1 0 0
      )
      'Error creating apple texture'##.Assert apple_texture
      texs←4×256*2
     
      bg_vb←##.SDL_CreateGPUBuffer core_data.device(
          ##.SDL_GPU_BUFFERUSAGE_VERTEX ⋄ bg_vs ⋄ 0
      )
      apple_vb←##.SDL_CreateGPUBuffer core_data.device(
          ##.SDL_GPU_BUFFERUSAGE_VERTEX ⋄ apple_vs ⋄ 0
      )
      ib←##.SDL_CreateGPUBuffer core_data.device(
          ##.SDL_GPU_BUFFERUSAGE_INDEX ⋄ is ⋄ 0
      )
     
      'Error creating menu buffers'##.Assert bg_vb ib
     
      sam_params←⊂0 0 0 0 0 2 0 4 0 0 200 1 0 0 0 0
      bg_sampler←##.SDL_CreateGPUSampler core_data.device sam_params
      sam_params←⊂0 0 0 2 2 2 0 4 0 0 200 1 0 0 0 0
      apple_sampler←##.SDL_CreateGPUSampler core_data.device sam_params
      'Error creating menu samplers'##.Assert bg_sampler apple_sampler
     
      Create_pipeline(
          core_data.device
          core_data.window
      )
     
      u←core_data.w÷64
      v←core_data.h÷64
      bg_vertices←∊[
          0 0 0 v dirt
          core_data.w 0 u v dirt
          core_data.w core_data.h u 0 dirt
          0 core_data.h 0 0 dirt
      ]
      apple_vertices←∊[
          0 0 0 1 dirt
          256 0 1 1 dirt
          256 256 1 0 dirt
          0 256 0 0 dirt
      ]
      indices←0 1 2 0 2 3
     
      tb←##.SDL_CreateGPUTransferBuffer(
          core_data.device
          ##.SDL_GPU_TRANSFERBUFFERUSAGE_UPLOAD(+/bg_vs apple_vs is texs)0
      )
      ptr←##.SDL_MapGPUTransferBuffer core_data.device tb 0
      ##.LSE_MemcpyF32(ptr ⋄ bg_vertices ⋄ bg_vs)
      ##.LSE_MemcpyF32(ptr+bg_vs ⋄ apple_vertices ⋄ apple_vs)
      ##.LSE_MemcpyU16(ptr+bg_vs+apple_vs ⋄ indices ⋄ is)
     
      :Section image_load
          ⍝ Magic number for 8 8 8 8 A B G R image encoding
          enc←376840196
          img←##.IMG_Load⊂'assets/logo.png'
          img2←##.SDL_ConvertSurface img enc
          'Error loading logo'##.Assert img img2
          ##.SDL_DestroySurface img
          img←##.SDL_ScaleSurface img2 256 256 0
          addr←##.LSE_GetSurfaceDataAddress img
          ##.SDL_DestroySurface img2
          ##.SDL_memcpy(ptr+bg_vs+apple_vs+is ⋄ addr ⋄ texs)
          ##.SDL_DestroySurface img
      :EndSection image_load
     
      ##.SDL_UnmapGPUTransferBuffer core_data.device tb
     
      cmd←##.SDL_AcquireGPUCommandBuffer core_data.device
      pass←##.SDL_BeginGPUCopyPass cmd
      ##.SDL_UploadToGPUBuffer pass(tb 0)(bg_vb 0 bg_vs)0
      ##.SDL_UploadToGPUBuffer pass(tb bg_vs)(apple_vb 0 apple_vs)0
      ##.SDL_UploadToGPUBuffer pass(tb ⋄ bg_vs+apple_vs)(ib 0 is)0
      ##.SDL_UploadToGPUTexture(
          pass
          tb(bg_vs+apple_vs+is)256 0
          apple_texture 0 0 0 0 0 256 256 1
          0
      )
      ##.SDL_EndGPUCopyPass pass
      ##.SDL_SubmitGPUCommandBuffer cmd
     
     
      txt←'UTF-8'⎕UCS'The Meaning of life',(⎕UCS 10),'⍎⊖⍕⊃⊂|⌊-*+○⌈×÷!⌽⍉⌹~⍴⍋⍒,⍟?⍳0'
      text_render←##.TTF_CreateText ##.text.engine ##.text.font 0 0
      ##.text.table⍪¨←(text_render ⋄ ⊆txt ⋄ 920 ⋄ 565 ⋄ 0.4 ⋄ 0.3 ⋄ (4⍴256)⊥212 175 55 255 ⋄ 0 ⋄ 0 ⋄ 1 ⋄ 0)
     
      text_render←##.TTF_CreateText ##.text.engine ##.text.font 0 0
      ##.text.table⍪¨←(text_render ⋄ ⊆'UTF-8'⎕UCS'Play' ⋄ 1280÷2 ⋄ 250 ⋄ 0.4 ⋄ 0 ⋄ (4⍴256)⊥255 255 255 255 ⋄ 0 ⋄ 0 ⋄ 1 ⋄ 0)
     
      text_render←##.TTF_CreateText ##.text.engine ##.text.font 0 0
      ##.text.table⍪¨←(text_render ⋄ ⊆'UTF-8'⎕UCS'Quit' ⋄ 1280÷2 ⋄ 180 ⋄ 0.4 ⋄ 0 ⋄ (4⍴256)⊥255 255 255 255 ⋄ 0 ⋄ 0 ⋄ 1 ⋄ 0)
     
    ∇

    ∇ Input core_data
     
    ∇

    ∇ Update core_data;click;x;y;buttons;inside;above;below;m
      elapsed+←core_data.dt
      (0⊃(##.text.Ti'scale')⊃⍉##.text.table)←0.5+0.025×1○3×elapsed
     
      (click x y) ← ##.SDL_GetMouseState 0 0
      y ← core_data.h - y

      buttons←(⊂1 2)⌷(⍉↑)##.text.table⌷⍨⊂##.text.tnames⍳'xywh'
      buttons←(0.4∘×@2 3)⍤1⊢buttons ⍝ hardcoded scale for now...
      buttons←((2 2)∘⍴⍤1)({(⍵+¯4↑2↑⍵)-0.5×(,⍨2↓⍵)}⍤1)buttons
      below←(0∘⌷⍤⊖⍤2)(x y)(<⍤1)buttons
      above←(0∘⌷⍤2)(x y)(>⍤1)buttons
      m←∧/below∧above
      ⍝ If add more non-clickable text (like flavor text) should modify methinks
      ((0,m)/(##.text.Ti'color')⊃##.text.table) ← (4⍴256)⊥212 175 55 255
      ((0,~m)/(##.text.Ti'color')⊃##.text.table) ← (4⍴256)⊥255 255 255 255

      :If (click=1)∧(0∊⍸m)

      :ElseIf (click=1)∧(1∊⍸m)
        core_data.quit←1
      :EndIf
    ∇

    ∇ Draw core_data;cmd_buf;res;swap;pass;color_info;ui_proj;mat;x;y;scale
      cmd_buf←##.SDL_AcquireGPUCommandBuffer core_data.device
     
      pass←##.SDL_BeginGPUCopyPass cmd_buf
      ##.text.Copy core_data.device pass
      ##.SDL_EndGPUCopyPass pass
     
      (res swap _ _)←##.SDL_WaitAndAcquireGPUSwapchainTexture(
          cmd_buf
          core_data.window
          0 ⋄ 0 ⋄ 0
      )
     
      ui_proj←##.math.Ortho(0 0 720 1280 0.1 100)
     
      :If swap≠0
          color_info←⊂swap 0 0(0 0 0 0)0 0 0 0 0 0 0 0 0
          pass←##.LSE_BeginDepthLessRenderPass cmd_buf color_info 1
          ##.SDL_BindGPUGraphicsPipeline pass pipeline
     
          ⍝ Background
          ##.SDL_PushGPUVertexUniformData cmd_buf 0(∊ui_proj)(16×4)
          ##.SDL_BindGPUVertexBuffers pass 0(⊂bg_vb 0)1
          ##.SDL_BindGPUIndexBuffer pass(⊂ib 0)##.SDL_GPU_INDEXELEMENTSIZE_16BIT
          ##.SDL_BindGPUFragmentSamplers pass 0(⊂##.block_data.texture bg_sampler)1
          ##.SDL_DrawGPUIndexedPrimitives pass 6 1 0 0 0
     
          ⍝ Logo
     
          scale←1@(⊂3 3)⊢1.1×∘.=⍨⍳4
          x←128-⍨1280÷2
          y←350
          mat←ui_proj+.×⍨scale+.×(x ⋄ y ⋄ 0 ⋄ 1)@3⊢∘.=⍨⍳4
          ##.SDL_PushGPUVertexUniformData cmd_buf 0(∊mat)(16×4)
          ##.SDL_BindGPUVertexBuffers pass 0(⊂apple_vb 0)1
          ##.SDL_BindGPUIndexBuffer pass(⊂ib 0)##.SDL_GPU_INDEXELEMENTSIZE_16BIT
          ##.SDL_BindGPUFragmentSamplers pass 0(⊂apple_texture apple_sampler)1
          ##.SDL_DrawGPUIndexedPrimitives pass 6 1 0 0 0
     
     
          ##.text.Draw cmd_buf pass ui_proj
          ##.SDL_EndGPURenderPass pass
      :EndIf
     
      ##.SDL_SubmitGPUCommandBuffer cmd_buf
    ∇


    ∇ Release core_data
      {##.SDL_ReleaseGPUBuffer core_data.device ⍵}¨bg_vb apple_vb ib
      ##.SDL_ReleaseGPUTexture core_data.device apple_texture
     
    ∇

    ∇ r←Create_shaders device;v_src;v_size;f_src;f_size;v_info;f_info;entry_point;shader_format;v_shader;f_shader
      (v_src v_size)←##.SDL_LoadFile(##.Get_shader_path'ui_vert')0
      (f_src f_size)←##.SDL_LoadFile(##.Get_shader_path'ui_frag')0
     
      'Error loading shaders'##.Assert v_src f_src
      shader_format←##.Get_shader_format ⍬
      v_info←v_size v_src 0 shader_format ##.SDL_GPU_SHADERSTAGE_VERTEX 0 0 0 1 0
      f_info←f_size f_src 0 shader_format ##.SDL_GPU_SHADERSTAGE_FRAGMENT 1 0 0 0 0
      entry_point←##.Get_entry_point ⍬
     
      v_shader←##.LSE_CreateGPUShader device v_info entry_point
      f_shader←##.LSE_CreateGPUShader device f_info entry_point
      'Error creating shaders'##.Assert v_shader f_shader
      r←(v_shader f_shader)
    ∇

    ∇ Create_pipeline(device window);v_shader;format;f_shader;vb_desc;vb_attr;blend_state;ct_desc
      (v_shader f_shader)←Create_shaders device
      format←##.SDL_GetGPUSwapchainTextureFormat device window
      ##.LSE_PipelineClearParams
      ##.LSE_PipelineSetShaders v_shader f_shader
     ⍝ 20=4×≢ x y u v w
      vb_desc←⊂(0 20 0 0)
      vb_attr←(0 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT2 0
          1 0 ##.SDL_GPU_VERTEXELEMENTFORMAT_FLOAT3(4×2))
      ##.LSE_PipelineSetVertexInput vb_desc 1 vb_attr 2
     
      blend_state←(
          ##.SDL_GPU_BLENDFACTOR_SRC_ALPHA
          ##.SDL_GPU_BLENDFACTOR_ONE_MINUS_SRC_ALPHA
          ##.SDL_GPU_BLENDOP_ADD
          ##.SDL_GPU_BLENDFACTOR_SRC_ALPHA
          ##.SDL_GPU_BLENDFACTOR_DST_ALPHA
          ##.SDL_GPU_BLENDOP_ADD
      ),16,1,3⍴0
      ct_desc←⊂format blend_state
      ##.LSE_PipelineSetTargetInfo ct_desc 1 0 0
      pipeline←##.LSE_PipelineCreate device
      'Error creating ui pipeline'##.Assert pipeline
      ##.SDL_ReleaseGPUShader device v_shader
      ##.SDL_ReleaseGPUShader device f_shader
    ∇
:EndNamespace
